apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
sourceCompatibility = 1.7
version = '1.0'
compileJava.options.encoding = 'UTF-8'
mainClassName = 'main.Main'

jar {
    manifest {
        attributes 'Implementation-Title': 'Lego Robotik Praktikum KIT 2016', 'Implementation-Version': version, 'Main-Class': mainClassName
    }
      // note this builds one fat jar and it is not recommended for production use - just for illustration purpose
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

configurations {
	sshAntTask
	provided
}


sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}
repositories {
	mavenCentral()
     flatDir {
       dirs  System.getenv('EV3_HOME')+'/lib/ev3'
   }
}


dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.7.1', 'jsch:jsch:0.1.29'
    provided group: 'lejos', name: 'ev3classes', version: '1.0'
    testCompile group: 'junit', name: 'junit', version: '4.+'

}

test {
    systemProperties 'property': 'value'
}

task deployEV3 << {
	ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp', classpath: configurations.sshAntTask.asPath)
	ant.scp(todir: ev3_username+'@'+ev3_server+':/home/lejos/programs',
		password: ev3_password,
		verbose: 'true') {
			fileset(dir: './build/libs') {
				include(name: '**/*.jar')
			}
		}
}
task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
